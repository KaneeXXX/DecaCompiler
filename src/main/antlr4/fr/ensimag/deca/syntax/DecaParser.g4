parser grammar DecaParser;

options {
    // Default language but name it anyway
    //
    language  = Java;

    // Use a superclass to implement all helper
    // methods, instance variables and overrides
    // of ANTLR default methods, such as error
    // handling.
    //
    superClass = AbstractDecaParser;

    // Use the vocabulary generated by the accompanying
    // lexer. Maven knows how to work out the relationship
    // between the lexer and parser and will build the
    // lexer before the parser. It will also rebuild the
    // parser if the lexer changes.
    //
    tokenVocab = DecaLexer;

}

// which packages should be imported?
@header {
    import fr.ensimag.deca.tree.*;
    import java.io.PrintStream;
}

@members {
    @Override
    protected AbstractProgram parseProgram() {
        return prog().tree;
    }
}

prog returns[AbstractProgram tree]
    : list_classes main EOF {
            assert($list_classes.tree != null);
            assert($main.tree != null);
            $tree = new Program($list_classes.tree, $main.tree);
            setLocation($tree, $list_classes.start);
        }
    ;

main returns[AbstractMain tree]
    : /* epsilon */ {
            $tree = new EmptyMain();
        }
    | block {
            assert($block.decls != null);
            assert($block.insts != null);
            $tree = new Main($block.decls, $block.insts);
            setLocation($tree, $block.start);
        }
    ;

block returns[ListDeclVar decls, ListInst insts]
    : OBRACE list_decl list_inst CBRACE {
            assert($list_decl.tree != null);
            assert($list_inst.tree != null);
            $decls = $list_decl.tree;
            $insts = $list_inst.tree;
        }
    ;

list_decl returns[ListDeclVar tree]
@init   {
            $tree = new ListDeclVar();
        }
    : decl_var_set[$tree]*
    ;

decl_var_set[ListDeclVar l]
    : type list_decl_var[$l,$type.tree] SEMI
    ;

list_decl_var[ListDeclVar l, AbstractIdentifier t]
    : dv1=decl_var[$t] {
        $l.add($dv1.tree);
        setLocation($dv1.tree, $dv1.start);
        } (COMMA dv2=decl_var[$t] {
            $l.add($dv2.tree);
            setLocation($dv2.tree, $dv2.start);
        }
      )*
    ;

decl_var[AbstractIdentifier t] returns[AbstractDeclVar tree]
@init   {
    AbstractInitialization init = new NoInitialization();
        }
    : i=ident {
        assert($i.tree != null);
        setLocation($i.tree, $i.start);
        }
      (EQUALS e=expr {
        assert($e.tree != null);
        init = new Initialization($e.tree);
        setLocation($e.tree, $e.start);
        }
      )? {
            $tree = new DeclVar($t, $i.tree, init);        
        }
    ;

list_inst returns[ListInst tree]
@init   {
    $tree = new ListInst();
}
    : (inst {
            assert($inst.tree != null);
            $tree.add($inst.tree);
            setLocation($inst.tree, $inst.start);
        }
      )*
    ;

inst returns[AbstractInst tree]
    : e1=expr SEMI {
            assert($e1.tree != null);
            $tree = $expr.tree;
        }
    | SEMI {
        }
    | PRINT OPARENT list_expr CPARENT SEMI {
            assert($list_expr.tree != null);
            $tree = new Print(false, $list_expr.tree);
        }
    | PRINTLN OPARENT list_expr CPARENT SEMI {
            assert($list_expr.tree != null);
            $tree = new Println(false, $list_expr.tree);
        }
    | PRINTX OPARENT list_expr CPARENT SEMI {
            assert($list_expr.tree != null);
            $tree = new Print(true, $list_expr.tree);
        }
    | PRINTLNX OPARENT list_expr CPARENT SEMI {
            assert($list_expr.tree != null);
            $tree = new Println(true, $list_expr.tree);
        }
    | if_then_else {
            assert($if_then_else.tree != null);
            $tree = $if_then_else.tree;
        }
    | WHILE OPARENT condition=expr CPARENT OBRACE body=list_inst CBRACE {
            assert($condition.tree != null);
            assert($body.tree != null);
            $tree = new While($condition.tree, $body.tree);
        }
    | RETURN expr SEMI {
            assert($expr.tree != null);
            $tree = new Return($expr.tree);

        }
    ;

if_then_else returns[IfThenElse tree]
@init {
    ListInst elseList = new ListInst();
    ListInst elseBranch;
    IfThenElse node;
}
    : if1=IF OPARENT condition=expr CPARENT OBRACE li_if=list_inst CBRACE {
        assert($condition.tree != null);
        assert($li_if.tree != null);
        $tree = new IfThenElse($condition.tree, $li_if.tree, elseList);
        node = $tree;
        }
      (ELSE elsif=IF OPARENT elsif_cond=expr CPARENT OBRACE elsif_li=list_inst CBRACE {
        assert($elsif_cond.tree != null);
        assert($elsif_li.tree != null);
        elseBranch = new ListInst();
        node = new IfThenElse($elsif_cond.tree, $elsif_li.tree, elseBranch);
        elseList.add(node);
        elseList = elseBranch;
        }
      )*
      (ELSE OBRACE li_else=list_inst CBRACE {
        assert($li_else.tree != null);
        node.setElseBranch($li_else.tree);
        }
      )?
    ;

list_expr returns[ListExpr tree]
@init   {
        $tree = new ListExpr();
        }
    : (e1=expr {
        assert($e1.tree != null);
        $tree.add($e1.tree);
        }
       (COMMA e2=expr {
            assert($e2.tree != null);
            $tree.add($e2.tree);
        }
       )* )?
    ;

expr returns[AbstractExpr tree]
    : assign_expr {
            assert($assign_expr.tree != null);
            $tree = $assign_expr.tree;
        }
    ;

assign_expr returns[AbstractExpr tree]
    : e=or_expr (
        /* condition: expression e must be a "LVALUE" */ {
            if (! ($e.tree instanceof AbstractLValue)) {
                throw new InvalidLValue(this, $ctx);
            }
            $tree = $e.tree;
            setLocation($e.tree, $e.start);
        }
        EQUALS e2=assign_expr {
            assert($e.tree != null);
            assert($e2.tree != null);
            $tree = new Assign((AbstractLValue) $e.tree, $e2.tree);
            setLocation($e2.tree, $e2.start);
        }
      | /* epsilon */ {
            assert($e.tree != null);
            $tree = $e.tree;
        }
      )
    ;

or_expr returns[AbstractExpr tree]
    : e=and_expr {
            assert($e.tree != null);
            $tree = $e.tree;
            setLocation($e.tree, $e.start);
        }
    | e1=or_expr OR e2=and_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Or($e1.tree, $e2.tree);
            setLocation($e1.tree, $e1.start);
            setLocation($e2.tree, $e2.start);
       }
    ;

and_expr returns[AbstractExpr tree]
    : e=eq_neq_expr {
            assert($e.tree != null);
            $tree = $e.tree;
            setLocation($e.tree, $e.start);
        }
    |  e1=and_expr AND e2=eq_neq_expr {
            assert($e1.tree != null);                         
            assert($e2.tree != null);
            $tree = new And($e1.tree, $e2.tree);
            setLocation($e1.tree, $e1.start);
            setLocation($e2.tree, $e2.start);
        }
    ;

eq_neq_expr returns[AbstractExpr tree]
    : e=inequality_expr {
            assert($e.tree != null);
            $tree = $e.tree;
            setLocation($e.tree, $e.start);
        }
    | e1=eq_neq_expr EQEQ e2=inequality_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Equals($e1.tree, $e2.tree);
            setLocation($e1.tree, $e1.start);
            setLocation($e2.tree, $e2.start);
        }
    | e1=eq_neq_expr NEQ e2=inequality_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new NotEquals($e1.tree, $e2.tree);
            setLocation($e1.tree, $e1.start);
            setLocation($e2.tree, $e2.start);
        }
    ;

inequality_expr returns[AbstractExpr tree]
    : e=sum_expr {
            assert($e.tree != null);
            $tree = $e.tree;
            setLocation($e.tree, $e.start);
        }
    | e1=inequality_expr LEQ e2=sum_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new LowerOrEqual($e1.tree, $e2.tree);
            setLocation($e1.tree, $e1.start);
            setLocation($e2.tree, $e2.start);
        }
    | e1=inequality_expr GEQ e2=sum_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new GreaterOrEqual($e1.tree, $e2.tree);
            setLocation($e1.tree, $e1.start);
            setLocation($e2.tree, $e2.start);
        }
    | e1=inequality_expr GT e2=sum_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Greater($e1.tree, $e2.tree);
            setLocation($e1.tree, $e1.start);
            setLocation($e2.tree, $e2.start);
        }
    | e1=inequality_expr LT e2=sum_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Lower($e1.tree, $e2.tree);
            setLocation($e1.tree, $e1.start);
            setLocation($e2.tree, $e2.start);
        }
    | e1=inequality_expr INSTANCEOF t=type {
            assert($e1.tree != null);
            assert($type.tree != null);
            $tree = new InstanceOf($e1.tree, $t.tree);
            setLocation($e1.tree, $e1.start);
            setLocation($e2.tree, $e2.start);
        }
    ;


sum_expr returns[AbstractExpr tree]
    : e=mult_expr {
            assert($e.tree != null);
            $tree = $e.tree;
            setLocation($e.tree, $e.start);
        }
    | e1=sum_expr PLUS e2=mult_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Plus($e1.tree, $e2.tree);
            setLocation($e1.tree, $e1.start);
            setLocation($e2.tree, $e2.start);
        }
    | e1=sum_expr MINUS e2=mult_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Minus($e1.tree, $e2.tree);
            setLocation($e1.tree, $e1.start);
            setLocation($e2.tree, $e2.start);
        }
    ;

mult_expr returns[AbstractExpr tree]
    : e=unary_expr {
            assert($e.tree != null);
            $tree = $e.tree;
            setLocation($e.tree, $e.start);
        }
    | e1=mult_expr TIMES e2=unary_expr {
            assert($e1.tree != null);                                         
            assert($e2.tree != null);
            $tree = new Multiply($e1.tree, $e2.tree);
            setLocation($e1.tree, $e1.start);
            setLocation($e2.tree, $e2.start);
        }
    | e1=mult_expr SLASH e2=unary_expr {
            assert($e1.tree != null);                                         
            assert($e2.tree != null);
            $tree = new Divide($e1.tree, $e2.tree);
            setLocation($e1.tree, $e1.start);
            setLocation($e2.tree, $e2.start);
        }
    | e1=mult_expr PERCENT e2=unary_expr {
            assert($e1.tree != null);                                                                          
            assert($e2.tree != null);
            $tree = new Modulo($e1.tree, $e2.tree);
            setLocation($e1.tree, $e1.start);
            setLocation($e2.tree, $e2.start);
        }
    ;

unary_expr returns[AbstractExpr tree]
    : op=MINUS e=unary_expr {
            assert($e.tree != null);
            $tree = new UnaryMinus($e.tree);
        }
    | op=EXCLAM e=unary_expr {
            assert($e.tree != null);
            $tree = new Not($e.tree);

        }
    | select_expr {
            assert($select_expr.tree != null);
            $tree = $select_expr.tree;
        }
    ;

select_expr returns[AbstractExpr tree]
    : e=primary_expr {
            assert($e.tree != null);
            $tree = $e.tree;
        }
    | e1=select_expr DOT i=ident {
            assert($e1.tree != null);
            assert($i.tree != null);
            setLocation($i.tree, $i.start);

        }
        (o=OPARENT args=list_expr CPARENT {
            // we matched "e1.i(args)"
            assert($args.tree != null);
            $tree = new MethodCall($e1.tree, $i.tree, $args.tree);
        }
        | /* epsilon */ {
            // we matched "e.i"
            $tree = new Selection($e1.tree, $i.tree);
        }
        )
    ;

primary_expr returns[AbstractExpr tree]
    : ident {
            assert($ident.tree != null);
            $tree = $ident.tree;
            setLocation($ident.tree, $ident.start);
        }
    | m=ident OPARENT args=list_expr CPARENT {
            assert($args.tree != null);
            assert($m.tree != null);
            $tree = $m.tree;
            setLocation($m.tree, $m.start);
        }
    | OPARENT expr CPARENT {
            assert($expr.tree != null);
            $tree = $expr.tree;
        }
    | READINT OPARENT CPARENT {
        $tree = new ReadInt();
        }
    | READFLOAT OPARENT CPARENT {
        $tree = new ReadFloat();
        }
    | NEW ident OPARENT CPARENT {
            assert($ident.tree != null);
            $tree = new New($ident.tree);
            setLocation($ident.tree, $ident.start);
        }
    | cast=OPARENT type CPARENT OPARENT expr CPARENT {
            assert($type.tree != null);
            assert($expr.tree != null);
            $tree = new Cast($type.tree, $expr.tree);
        }
    | literal {
            assert($literal.tree != null);
            $tree = $literal.tree;
            setLocation($literal.tree, $literal.start);
        }
    ;

type returns[AbstractIdentifier tree]

    : ident {
            assert($ident.tree != null);
            $tree = $ident.tree;
            setLocation($ident.tree, $ident.start);
        }
    ;

literal returns[AbstractExpr tree]
    : INT {
        $tree = new IntLiteral(Integer.parseInt($INT.text));
        }
    | fd=FLOAT {
        $tree = new FloatLiteral(Float.parseFloat($fd.text));
        }
    | STRING {
        $tree = new StringLiteral($STRING.text);
        }
    | TRUE {
        $tree = new BooleanLiteral(true);
        }
    | FALSE {
        $tree = new BooleanLiteral(false);
        }
    | THIS {
        $tree = new This();
        }
    | NULL {
        $tree = new Null();
        }
    ;

ident returns[AbstractIdentifier tree]
    : IDENT {
            $tree = new Identifier(getDecacCompiler().createSymbol($IDENT.getText()));
        }
    ;

/****     Class related rules     ****/

list_classes returns[ListDeclClass tree]
@init   {
        $tree = new ListDeclClass();
    }
    :
      (c1=class_decl {
        assert($c1.tree != null);
        $tree.add($c1.tree);
        setLocation($c1.tree, $c1.start);
        }
      )*
    ;

class_decl returns[DeclClass tree]
    : CLASS name=ident superclass=class_extension OBRACE class_body CBRACE {
        assert($name.tree != null);
        assert($superclass.tree != null);
        assert($class_body.a != null);
        assert($class_body.b != null);
        $tree = new DeclClass($name.tree, $superclass.tree, $class_body.b, $class_body.a);
        setLocation($name.tree, $name.start);
        setLocation($superclass.tree, $superclass.start);
        }
    ;

class_extension returns[AbstractIdentifier tree]
    : EXTENDS ident {
        assert($ident.tree != null);
        $tree = $ident.tree;
        setLocation($ident.tree, $ident.start);
        }
    | /* epsilon */ {
        $tree = new Identifier(getDecacCompiler().createSymbol("Object"));
        }
    ;

class_body returns[ListDeclMethod a, ListDeclField b]
@init {
    $a = new ListDeclMethod();
    $b = new ListDeclField();
}
    : (m=decl_method[$a] {
        }
      | decl_field_set[$b]
      )* 
    ;

decl_field_set[ListDeclField l]
    : v=visibility t=type list_decl_field[$l, $t.tree, $v.tree]
      SEMI
    ;

visibility returns[Visibility tree]
    : /* epsilon */ {
        $tree = Visibility.PUBLIC;
        }
    | PROTECTED {
        $tree = Visibility.PROTECTED;
        }
    ;

list_decl_field [ListDeclField l, AbstractIdentifier t, Visibility v]
    : dv1=decl_field[$v, $t] {
        $l.add($dv1.tree);
        setLocation($dv1.tree, $dv1.start);
    }   
        (COMMA dv2=decl_field[$v, $t] {
            $l.add($dv2.tree);
            setLocation($dv2.tree, $dv2.start);
        }
      )*
    ;

decl_field[Visibility v, AbstractIdentifier t] returns[AbstractDeclField tree]
@init {
    AbstractInitialization init = new NoInitialization();
}
    : i=ident {
        assert($i.tree != null);
        setLocation($i.tree, $i.start);
        }
      (EQUALS e=expr {
        assert($e.tree != null);
        init = new Initialization($e.tree);
        setLocation($e.tree, $e.start);
        }
      )? {
        $tree = new DeclField($v, $t, $i.tree, init);
        }
    ;

decl_method[ListDeclMethod l]
@init {
    DeclMethod method;
    MethodBody body;
}
    : t=type i=ident OPARENT params=list_params CPARENT (b=block {
            method = new DeclMethod($t.tree, $i.tree, $params.l, new MethodBody($b.decls, $b.insts));
            setLocation($i.tree, $i.start);
        }
      | ASM OPARENT code=multi_line_string CPARENT SEMI {
            method = new DeclMethod($t.tree, $i.tree, $params.l, new MethodAsmBody($code.text));
        }
      ) {
          $l.add(method);
          setLocation(method, $t.start);
        }
    ;

list_params returns[ListDeclParam l]
@init {
    $l = new ListDeclParam();
}
    : (p1=param {
        $l.add($p1.tree);
        setLocation($p1.tree, $p1.start);
        } (COMMA p2=param {
            $l.add($p2.tree);
            setLocation($p2.tree, $p2.start);
        }
      )*)?
    ;
    
multi_line_string returns[String text, Location location]
    : s=STRING {
            $text = $s.text;
            $location = tokenLocation($s);
        }
    | s=MULTI_LINE_STRING {
            $text = $s.text;
            $location = tokenLocation($s);
        }
    ;

param returns[DeclParam tree]
    : t=type i=ident {
        assert($t.tree != null);
        assert($i.tree != null);
        $tree = new DeclParam($t.tree, $i.tree);
        setLocation($i.tree, $i.start);
        }
    ;
